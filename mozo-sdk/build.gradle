plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

ext {
    PUBLISH_ARTIFACT_ID = 'mozo-sdk'
    PUBLISH_GROUP_ID = 'io.mozocoin'
    PUBLISH_VERSION = '0.2.4-beta20'
    PUBLISH_DESCRIPTION = 'MozoSDK for Android'
    PUBLISH_URL = 'https://github.com/Biglabs/mozo-android-sdk'
}

android {
    compileSdkVersion COMPILE_VERSION
    defaultConfig {
        minSdkVersion MIN_VERSION
        targetSdkVersion TARGET_VERSION
        versionName PUBLISH_VERSION
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "SDK_VERSION", "\"$PUBLISH_VERSION\""
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    def kotlin_x_version = '1.4.3'
    def retrofit_version = '2.9.0'
    def room_version = '2.3.0'
    def lifecycle_version = '2.3.1'

    api fileTree(include: ['*.jar'], dir: 'libs')
    // Android X
    api 'androidx.browser:browser:1.3.0'
    api 'androidx.constraintlayout:constraintlayout:2.0.4'
    api 'androidx.core:core-ktx:1.5.0'
    api "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    api "androidx.preference:preference-ktx:1.1.1"
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'com.google.android.material:material:1.3.0'

    // Kotlin X
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_x_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_x_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // Crypto
    implementation("com.google.guava:guava:30.1-android")
    implementation 'org.bitcoinj:bitcoinj-core:0.15.3'
    implementation 'org.cryptonode.jncryptor:jncryptor:1.2.0'
    implementation("org.web3j:core:4.6.0-android") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    // To prevent Polymorphic Typing issue in FasterXML jackson-databind
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.3'

    // Other
    api 'com.journeyapps:zxing-android-embedded:4.1.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation 'org.java-websocket:Java-WebSocket:1.5.1'
    api 'io.michaelrocks:libphonenumber-android:8.12.22'
    api 'io.coil-kt:coil:1.2.2'

    // test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

apply from: "${rootDir}/scripts/publish-module.gradle"
