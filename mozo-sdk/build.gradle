plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

android {
    namespace 'io.mozocoin.sdk'
    compileSdkVersion COMPILE_VERSION
    defaultConfig {
        minSdkVersion MIN_VERSION
        targetSdkVersion TARGET_VERSION
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "SDK_VERSION", "\"$PUBLISH_VERSION\""
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    configurations {
        // Remove duplicate files in web3j 4.8.8-android
        all*.exclude module: 'bcprov-jdk15on'
    }

    buildFeatures {
        viewBinding true
    }
    lint {
        abortOnError false
        disable 'InvalidPackage'
    }
}

dependencies {
    def kotlin_x_version = '1.6.3'
    def retrofit_version = '2.9.0'
    def room_version = '2.4.2'
    def lifecycle_version = '2.5.0'
    def camera2_version = '1.1.0'

    api fileTree(include: ['*.jar'], dir: 'libs')
    // Android X
    api 'androidx.browser:browser:1.4.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'androidx.core:core-ktx:1.8.0'
    api "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    api "androidx.preference:preference-ktx:1.2.0"
    api 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.annotation:annotation:1.4.0'
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation 'com.google.android.gms:play-services-location:20.0.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.google.android.play:feature-delivery-ktx:2.0.0'

    // Kotlin X
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_x_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_x_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'com.google.code.gson:gson:2.9.0'

    // Crypto
    implementation 'com.google.guava:guava:31.1-android'
    implementation 'org.bitcoinj:bitcoinj-core:0.16.1'
    implementation 'org.cryptonode.jncryptor:jncryptor:1.2.0'
    implementation("org.web3j:core:5.0.0") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    // To prevent Polymorphic Typing issue in FasterXML jackson-databind
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'

    // Barcode scanning
    implementation 'com.google.zxing:core:3.5.0'
    implementation 'com.google.mlkit:barcode-scanning:17.0.2'
    implementation "androidx.camera:camera-camera2:${camera2_version}"
    implementation "androidx.camera:camera-lifecycle:${camera2_version}"
    implementation "androidx.camera:camera-view:${camera2_version}"

    // Other
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'org.java-websocket:Java-WebSocket:1.5.3'
    api 'io.michaelrocks:libphonenumber-android:8.12.51'
    api 'io.coil-kt:coil:2.1.0'

    // test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

apply from: "${rootDir}/scripts/publish-module.gradle"
